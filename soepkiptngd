#!/usr/bin/perl
############################################################################
# soepkiptngd (Soepkip The Next Generation daemon)
#
# (c) copyright 2000 Eric Lammerts <eric@lammerts.org>
#
# loosely based on `mymusic' by "caffiend" <caffiend@atdot.org>
# and `Soepkip' by Andre Pool <andre@scintilla.utwente.nl>
#
# $Id$
############################################################################
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# A copy of the GNU General Public License is available on the World Wide Web
# at `http://www.gnu.org/copyleft/gpl.html'.  You can also obtain it by
# writing to the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
############################################################################

$configfile = "/etc/soepkiptng.conf";
$ENV{'PATH'} = "/usr/local/bin:/usr/bin:/bin";

use DBI;
use MPEG::MP3Info;
use Socket;
use POSIX ":sys_wait_h";
use Sys::Hostname;
use Getopt::Std;

getopts('dr');
$debug = 1 if $opt_d;

open F, $configfile
	or die "$configfile: $!\n";
while(<F>) {
	/^#/ and next;
	s/\s+$//;
	/./ or next;
	if(/^(\w+)\s*=\s*(.*?)\s*$/) {
		$f = $1;
		${$f} = $2;
	} elsif(/^\s+(.*?)\s*$/) {
		# continuation line
		${$f} .= "\n$1";
	} else {
		die "$configfile line $.: invalid format\n";
	}
}
close F;

socket(KILLSOCK, PF_INET, SOCK_STREAM, getprotobyname('tcp'))
	or die "socket: $!\n";
setsockopt(KILLSOCK, SOL_SOCKET, SO_REUSEADDR, pack("l", 1));
bind(KILLSOCK, sockaddr_in(0, INADDR_ANY))
	or die "bind $killport: $!\n";
listen(KILLSOCK, SOMAXCONN);

$killhost = hostname;
($killport) = unpack_sockaddr_in(getsockname(KILLSOCK));

$dbh = DBI->connect("DBI:mysql:$db_name:$db_host", $db_user, $db_pass)
	or die "Can't connect to database $db_name\@$db_host as user $db_user\n";

sub rotatelog() {
	return if -s STDERR < 65000;
	rename $errfile, "$errfile.old" or do {
		warn "rename $errfile -> $errfile.old: $!\n";
		return;
	};
	close STDERR;
	open STDERR, ">$errfile";
}

sub warnrotate {
	printf STDERR "%s %s", scalar localtime, $_[0];
	rotatelog();
}

sub dierotate {
	printf STDERR "%s %s", scalar localtime, $_[0];
	rotatelog();
	exit 1;
}

unless($debug || $opt_r) {
	fork && exit;
	chdir "/";
	setpgrp();
	open STDIN, "</dev/null";
	open STDERR, ">>$errfile";
	$SIG{__DIE__} = \&dierotate;
	$SIG{__WARN__} = \&warnrotate;
}

$SIG{'CHLD'} = sub {
	for(;;) {
		my $p = waitpid(-1, &WNOHANG);
		return if $p < 1;
		warn sprintf "reaped child %d, sig=%d status=%d\n",
			$p, $? & 0x7f, $? >> 8;
		if($p == $cdrplaypid) {
			unlink $statusfile;
			die "exiting because '$playercmd' died.\n";
		} elsif($p == $pid) {
			$pid = 0;
			$pid_status = $? >> 8;
			$pid_signal = $? & 0x7f;
			warn "player finished ($p)\n";
		}
	}
};

warn "********** starting (pid=$$) **********\n";

$SIG{'TERM'} = sub {
	warn "got SIGTERM, killing $pid and $cdrplaypid, exiting\n";
	unlink $statusfile;
	kill 'KILL', $pid, $cdrplaypid;
	exit;
};

$SIG{'USR1'} = sub {
	$restart = 1;
};

unless($opt_r) {
	$cdrplaypid = open STDOUT, "|$playercmd";
	warn "cdrplaypid=$cdrplaypid\n";
}

$SIG{'PIPE'} = 'IGNORE';

for(;;) {
	my ($song_id, $songtype, $filename);

	if($restart) {
		# close-on-exec apparently doesn't work
		$dbh->disconnect;
		close SOCK if $socket_open;
		close KILLSOCK;

		warn "execing myself\n";
		exec $0, '-r';
		die "$0: $!\n";
	}

	# get queued song
	$dbh->do("LOCK TABLES queue WRITE, songs READ");
	($song_id, $song_order, $filename) = $dbh->selectrow_array(
		"SELECT queue.song_id,queue.song_order,songs.filename" .
		" FROM queue,songs" .
		" WHERE queue.song_id=songs.id" .
		" ORDER BY queue.song_order" .
		" LIMIT 1"
	);

	if($song_id) {
		$songtype = 'Q';
		warn "playing queued $filename\n";

		# delete it from the queue
		$dbh->do("DELETE FROM queue WHERE song_id = $song_id");
		$dbh->do("UPDATE queue SET song_order = song_order - $song_order - 1");
	}
	$dbh->do("UNLOCK TABLES");

	#  if nothing in queue, get random song
	if(!$song_id) {
		$songtype = 'R';
		($song_id, $filename) = $dbh->selectrow_array(
			"SELECT id,filename FROM songs " .
			"ORDER BY rand() LIMIT 1"
		);
		warn "playing random $filename\n";
	}

	if(!$song_id) {
		warn "no random song found.\n";
		sleep 5;
		next;
	}

	# write to log file
	logprintf("%s %6d %s", $songtype, $song_id, $filename);

	# write status file
	if(open ST, ">$statusfile") {
		print ST <<EOF;
$song_id
$filename
$$
$cdrplaypid
$killhost
$killport
EOF
		close ST;
	} else {
		warn "cannot open statusfile $statusfile: $!\n";
	}

	# close accepted socket after statusfile was updated
	if($socket_open) {
		print SOCK <<EOF;
$song_id
$filename
$$
$cdrplaypid
$killhost
$killport
EOF
		close SOCK;
		$socket_open = 0;
	}

	# get file type
	$filename =~ /([^.]*)$/;
	$ext = lc($1);

	# launch player
	$starttime = time;
	if($debug) {
		($a, $b, $c, $d) = times;
		$childtime = $c + $d;
	}
	if(($pid = fork) == 0) {
		$SIG{'PIPE'} = 'DEFAULT';
		if($ext eq "mp2" || $ext eq "mp3") {
			exec "mp3toraw", $filename;
		} elsif($ext eq "ogg") {
			exec "ogg2raw", $filename;
		} elsif($ext eq "wav") {
			exec "splay", "-d", "-", $filename;
		} elsif($ext eq "pac") {
			exec "lpac2raw", $filename;
		} else {
			warn "no player for .$ext files.\n";
		}
		die "exec failed";
	}
	warn "pid=$pid\n";

	# wait until player is done or we get a connect on KILLSOCK
	vec($rin, fileno(KILLSOCK), 1) = 1;
	while($pid) {
		if(select($rout = $rin, undef, undef, 0.1) == 1) {
			warn "got connection\n";

			# kill player
			kill 'KILL', $pid
				or warn "kill -KILL $pid: $!\n";

			# tell cdrplay to flush its buffers
			kill 'SIGUSR1', $cdrplaypid
				or warn "kill -USR1 $cdrplaypid: $!\n";

			# accept the tcp connection; we close it later,
			# after a new song has been selected
			accept SOCK, KILLSOCK and $socket_open = 1;

			# write to log file
			logprintf("K %6d", $song_id);
		}
	}
	if(($pid_status || $pid_signal) && !$socket_open) {
		# write to log file
		logprintf("E %6d status=%d signal=%d", $song_id, $pid_status, $pid_signal);
	}

	if($debug) {
		($a, $b, $c, $d) = times;
		$childtime = $c + $d - $childtime;
		warn "song finished, time=$childtime\n";
	}

	# if it took less than 1 second to play it, pause for 1 second
	# to prevent us from eating 100% cpu time in case of misconfiguration
	if(time == $starttime) {
		sleep 1;
	}
}

sub logprintf($@) {
	my ($fmt, @args) = @_;

	# write to log file
	if(open LOG, ">>$logfile") {
		printf LOG "%s $fmt\n", scalar localtime, @args;
		close LOG;
	} else {
		warn "cannot open logfile $logfile: $!\n";
	}
}
