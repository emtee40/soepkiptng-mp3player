#!/usr/bin/perl
############################################################################
# soepkiptng (c) copyright 2000 Eric Lammerts <eric@lammerts.org>.
############################################################################
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as 
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# A copy of the GNU General Public License is available on the World Wide Web
# at `http://www.gnu.org/copyleft/gpl.html'.  You can also obtain it by
# writing to the Free Software Foundation, Inc., 59 Temple Place - Suite 330,
# Boston, MA 02111-1307, USA.
############################################################################

use strict;
use Cwd 'abs_path';
use DBI;
use Getopt::Std;

# find program directory
$_ = $0;
while(-l) {
	my $l = readlink or die "readlink $_: $!\n";
	if($l =~ m|^/|) { $_ = $l; } else { s|[^/]*$|/$l|; }
}
m|(.*)/|;
my $progdir = abs_path($1);

require "$progdir/soepkiptng.lib";

our ($opt_f, $opt_q, $opt_h, $opt_x, $opt_c, $opt_n);
getopts('fqhxc:n');

my %conf;
read_configfile(\%conf, $opt_c);

$ENV{PATH} = "$progdir/bin:$ENV{PATH}";

if($opt_h) {
	print <<EOF;
Usage: soepkiptng_delete [-fqc] [dir]

Soepkiptng_delete deletes files from the SoepkipTNG database that do not exist
anymore.

    -f : delete entries even if they still exist
    -q : quiet
    -n : don't actually do anything to the database
    -x : permanently delete entries that are not present

EOF
	exit;
}

my $dbh = connect_to_db(\%conf);

my $sth = $dbh->prepare("SELECT filename FROM song WHERE present AND filename like ?");
my $dir = "/%";
if(@ARGV) {
	$dir = abs_path($ARGV[0]) or die "$ARGV[0]: $!\n";
	$dir .= "/%";
} elsif($opt_f) {
	die "-f not allowed without dir (to avoid accidents)\n";
}
$dir =~ s/\\/\\\\/g;
$sth->execute($dir);

while(my ($filename) = $sth->fetchrow_array) {
	if(!$opt_f) { -s $filename and next; }
	if($opt_n) {
		my $p = "permanently " if $opt_x;
		warn "Would ${p}delete $filename from database\n";
		next;
	}
	warn "Deleting $filename from database.\n" unless $opt_q;
	if($opt_x) {
		$dbh->do("DELETE FROM song WHERE filename=?", undef, $filename);
	} else {
		$dbh->do("UPDATE song SET present=0 WHERE filename=?", undef, $filename);
	}
}

$dbh->disconnect();
